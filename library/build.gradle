apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka-android'

group='com.github.Constructor-io'

jacoco {
    toolVersion = '0.8.1'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion 28

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
        versionCode 1
        versionName '2.8.0'
        buildConfigField("String", "CLIENT_VERSION", "\"cioand-${versionName}\"")
        buildConfigField("String", "DEFAULT_ITEM_SECTION", "\"Products\"")
        buildConfigField("String", "SERVICE_URL", "\"ac.cnstrc.com\"")
        buildConfigField("Integer", "SERVICE_PORT", "443")
        buildConfigField("String", "SERVICE_SCHEME", "\"https\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/MANIFEST.MF'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    androidExtensions {
        experimental = true
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            output.assemble.dependsOn lintTask
        }
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "./docs"
    }
}

task jacocoUnitTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

    def buildDir = "build/"

    def coverageSourceDirs = [
            "src/main/java"
    ]

    def excludedClasses = [
            '**/databinding/**/*.*',
            '**/android/databinding/*Binding.*',
            '**/BR.*',
            '**/R.*',
            '**/R$*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*_MembersInjector.*',
            '**/Dagger*Component.*',
            '**/Dagger*Component$Builder.*',
            '**/*Module_*Factory.*',
            '**/*Fragment*.*',
            '**/*Activity*.*',
            '**/*Adapter*.*',
            '**/*ViewPager*.*',
            '**/*ViewHolder*.*',
            '**/*Module*.*'
    ]

    def javaClasses = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: excludedClasses
    )

    def kotlinClasses = fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: excludedClasses
    )

    classDirectories = files([ javaClasses ], [ kotlinClasses ])
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = fileTree(dir: "$buildDir/jacoco", includes: [
            "testDebugUnitTest.exec"
    ])

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task getCoverage(type: Exec, dependsOn: 'jacocoUnitTestReport') {
    group = "Reporting"
    commandLine "open", "$buildDir/reports/jacoco/jacocoUnitTestReport/html/index.html"
}


apply from: 'dependencies.gradle'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    testImplementation 'io.mockk:mockk:1.9.kotlin12'
    testImplementation 'org.robolectric:robolectric:3.6.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.14.1'
    implementation supportLibs
    implementation networkLibs
    implementation otherLibs
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    kapt annotationProcessorLibs
    kaptTest daggerCompiler
    kaptAndroidTest daggerCompiler
    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs
}
